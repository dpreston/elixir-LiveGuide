# Structs

## Intro

In [chapter 7](/getting-started/keywords-and-maps.html) we learned about maps:

```elixir
map = %{a: 1, b: 2}
```

```elixir
map[:a]
```

```elixir
%{map | a: 3}
```

Structs are extensions built on top of maps that provide compile-time checks and default values.

## Defining structs

To define a struct, the `defstruct` construct is used:

```elixir
defmodule User do
  defstruct name: "John", age: 27
end
```

The keyword list used with `defstruct` defines what fields the struct will have along with their default values.

Structs take the name of the module they're defined in. In the example above, we defined a struct named `User`.

We can now create `User` structs by using a syntax similar to the one used to create maps (if you have defined the struct in a separate file, you can compile the file inside IEx before proceeding by running `c "file.exs"`; be aware you may get an error saying `the struct was not yet defined` if you try the below example in a file directly due to when definitions are resolved):

```elixir
%User{}
```

```elixir
%User{name: "Jane"}
```

Structs provide *compile-time* guarantees that only the fields (and *all* of them) defined through `defstruct` will be allowed to exist in a struct:

```elixir
%User{oops: :field}
```

## Accessing and updating structs

When we discussed maps, we showed how we can access and update the fields of a map. The same techniques (and the same syntax) apply to structs as well:

```elixir
john = %User{}
```

```elixir
john.name
```

```elixir
jane = %{john | name: "Jane"}
```

```elixir
%{jane | oops: :field}
```

When using the update syntax (`|`), the <abbr title="Virtual Machine">VM</abbr> is aware that no new keys will be added to the struct, allowing the maps underneath to share their structure in memory. In the example above, both `john` and `jane` share the same key structure in memory.

Structs can also be used in pattern matching, both for matching on the value of specific keys as well as for ensuring that the matching value is a struct of the same type as the matched value.

```elixir
%User{name: name} = john
```

```elixir
name
```

```elixir
%User{} = %{}
```

## Structs are bare maps underneath

In the example above, pattern matching works because underneath structs are bare maps with a fixed set of fields. As maps, structs store a "special" field named `__struct__` that holds the name of the struct:

```elixir
is_map(john)
```

```elixir
john.__struct__
```

Notice that we referred to structs as **bare** maps because none of the protocols implemented for maps are available for structs. For example, you can neither enumerate nor access a struct:

```elixir
john = %User{}
```

```elixir
john[:name]
```

```elixir
Enum.each(john, fn {field, value} -> IO.puts(value) end)
```

However, since structs are just maps, they work with the functions from the `Map` module:

```elixir
jane = Map.put(%User{}, :name, "Jane")
```

```elixir
Map.merge(jane, %User{name: "John"})
```

```elixir
Map.keys(jane)
```

Structs alongside protocols provide one of the most important features for Elixir developers: data polymorphism. That's what we will explore in the next chapter.

## Default values and required keys

If you don't specify a default key value when defining a struct, `nil` will be assumed:

```elixir
defmodule Product do
  defstruct [:name]
end
```

```elixir
%Product{}
```

You can define a structure combining both fields with explicit default values, and implicit `nil` values. In this case you must first specify the fields which implicitly default to nil:

```elixir
defmodule User do
  defstruct [:email, name: "John", age: 27]
end
```

```elixir
%User{}
```

Doing it in reverse order will raise a syntax error:

```elixir
defmodule User do
  defstruct [name: "John", age: 27, :email]
end
```

You can also enforce that certain keys have to be specified when creating the struct via the `@enforce_keys` module attribute:

```elixir
defmodule Car do
  @enforce_keys [:make]
  defstruct [:model, :make]
end
```

```elixir
%Car{}
```

Enforcing keys provides a simple compile-time guarantee to aid developers when building structs. It is not enforced on updates and it does not provide any sort of value-validation.
